prefix np: <http://www.nanopub.org/nschema#>
prefix npx: <http://purl.org/nanopub/x/>
prefix npa: <http://purl.org/nanopub/admin/>
prefix dct: <http://purl.org/dc/terms/>
prefix foaf: <http://xmlns.com/foaf/0.1/>

select distinct ?user ?name ?intronp ?date ?pubkey where {
  graph npa:graph {
    ?intronp npa:hasHeadGraph ?h .
    ?intronp npa:hasValidSignatureForPublicKey ?pubkey .
    ?intronp dct:created ?date .
  }
  graph ?h {
    ?intronp np:hasAssertion ?a .
  }
  graph ?a {
    ?keydeclaration npx:declaredBy ?user .
    optional {
      ?user foaf:name ?name .
    }
  }
  filter not exists {
    graph npa:graph {
      ?retraction npa:hasValidSignatureForPublicKey ?retractionpubkey .
      ?retraction npa:hasHeadGraph ?rh .
    }
    graph ?rh {
      ?retraction np:hasAssertion ?ra .
    }
    graph ?ra {
      ?somebody npx:retracts ?intronp .
    }
    filter (?retractionpubkey = ?pubkey)
  }
  filter not exists {
    graph npa:graph {
      ?superseding npa:hasValidSignatureForPublicKey ?supersedingpubkey .
      ?superseding npa:hasHeadGraph ?sh .
    }
    graph ?sh {
      ?superseding np:hasPublicationInfo ?si .
    }
    graph ?si {
      ?superseding npx:supersedes ?intronp .
    }
    filter (?supersedingpubkey = ?pubkey)
  }
}
order by ?user desc(?date)
