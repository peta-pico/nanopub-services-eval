prefix np: <http://www.nanopub.org/nschema#>
prefix npa: <http://purl.org/nanopub/admin/>
prefix npx: <http://purl.org/nanopub/x/>
prefix xsd: <http://www.w3.org/2001/XMLSchema#>
prefix dct: <http://purl.org/dc/terms/>

select ?np ?date ?pubkey ?superseded ?retracted where {
  graph npa:graph {
    ?np npa:hasHeadGraph ?h .
#    ?np npa:creationDay ?__day_iri .
#    ?np npa:creationMonth ?__month_iri .
#    ?np npa:creationYear ?__year_iri .
    ?np dct:created ?date .
    ?np npa:hasValidSignatureForPublicKey ?pubkey .  # ?__pubkey_xsd_string .
#    bind(?__pubkey_xsd_string as ?pubkey)
  }
  graph ?h {
    ?np ?__graphpred_iri ?g .
  }
  graph ?g {
    { <https://orcid.org/0000-0001-5118-256X> ?p ?o } union
    { ?s <https://orcid.org/0000-0001-5118-256X> ?o } union
    { ?s ?p <https://orcid.org/0000-0001-5118-256X> }
  }
  bind(exists {
    graph npa:graph {
      ?newversion npa:hasHeadGraph ?nh .
      ?newversion npa:hasValidSignatureForPublicKey ?pubkey .
    }
    graph ?nh {
      ?newversion np:hasPublicationInfo ?ni .
    }
    graph ?ni {
      ?newversion npx:supersedes ?np .
    }
  } as ?superseded )
  bind(exists {
    graph npa:graph {
      ?retraction npa:hasHeadGraph ?rh .
      ?retraction npa:hasValidSignatureForPublicKey ?pubkey .
    }
    graph ?rh {
      ?retraction np:hasAssertion ?ra .
    }
    graph ?ra {
      ?somebody npx:retracts ?np .
    }
  } as ?retracted )
}
limit 1000
